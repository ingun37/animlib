include(qtconfig)

# Tell qmake that this plugin needs the QUiTools module.
macx {
	# On OS X Qt currently installs libQtUiTools.a into /usr/lib rather
	# than into the Qt framework where all the other Qt libs reside. This
	# means that the Makefile generated by qmake won't look for the library
	# in the right place and will fail.
	# To get around that, we must include the library explicitly:
	#
	INCLUDEPATH += /usr/include/QtUiTools
	LIBS += /usr/lib/libQtUiTools.a
}

!macx {
	# On Linux and Windows the QtUiTools library is installed in the
	# correct place so we can just specify it in the CONFIG variable.
	CONFIG += uitools
}

TARGET = qtForms
HEADERS += qtForms.h
SOURCES += qtForms.cpp
FORMS += cubeForm.ui
RESOURCES += qtForms.qrc
